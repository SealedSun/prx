/*
 * Prexonite, a scripting engine (Scripting Language -> Bytecode -> Virtual Machine)
 *  Copyright (C) 2007  Christian "SealedSun" Klauser
 *  E-mail  sealedsun a.t gmail d.ot com
 *  Web     http://www.sealedsun.ch/
 *
 *  This program is free software; you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation; either version 2 of the License, or
 *  (at your option) any later version.
 *
 *  Please contact me (sealedsun a.t gmail do.t com) if you need a different license.
 * 
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License along
 *  with this program; if not, write to the Free Software Foundation, Inc.,
 *  51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
 */

Name prx\lib;
Description "Provides common routines for prx.";
Author SealedSun;
	
declare println as echo;

//Access to an experimental terminal
declare command __console;

//Not all environments may support colors...
var supportsColors = (supportsColors ?? true)~Bool;

//Run 'f' in a context where console output has the color 'color'.
function runInDifferentColor(ref f, color)
{
    if(supportsColors)
    {
    	var orig = ::Console.ForegroundColor;
    	::Console.ForegroundColor = color;
    	var r = f();
    	::Console.ForegroundColor = orig;
    }
    else
    {
        var r = f();   
    }
    return r;
}

//Shortcuts for commonly used colorations
ref red = 
    if(supportsColors)
        f => runInDifferentColor(f, ::ConsoleColor.Red)
    else
        f => f.()
    ;
ref green = 
    if(supportsColors)
        f => runInDifferentColor(f, ::ConsoleColor.Green)
    else
        f => f.()
    ;
ref yellow =
    if(supportsColors)
        f => runInDifferentColor(f, ::ConsoleColor.Yellow)
    else
        f => f.()
    ;

var isWindowsNT = System::Environment.OSVersion.Platform~Int == System::PlatformID.Win32NT~Int; 
  
function __consoleAvailable = asm(ldr.eng).Commands.Contains("__console");
  
var supportsTabs = (supportsTabs ?? (isWindowsNT && __consoleAvailable))~Bool;
    
function readline = 
    if(supportsTabs)
        __console.ReadLine
    else
        System::Console.ReadLine;
    
//Show prompt and read line
var prompt\color; 
function showPrompt(q) does
{
	declare var prompt\color as color;
	var originalColor;
    if(q == null)
        q = "PRX> ";
    runInDifferentColor( () => print(q), 
        if(color != null) color else ::Console.ForegroundColor);
	return readline;
}

function report
{
    foreach(var arg in var args)
    {
        if(arg is not Prexonite::Types::PValueKeyValuePair)
            throw "Arguments must be key value pairs!";
            
        var msg = arg.Key;
        var act = arg.Value;
        
        print("$msg...");
        var r = act.();
        println("done.");
    }
    return r;
}

var prompt = null;

//Functions that provide access to the currently executing engine and application.
function thisEngine = asm ( ldr.eng );
function thisApplication = asm ( ldr.app );