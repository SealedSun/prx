
build does require = "struct.pxs";

function create_set()[is volatile;]
{
    var store = [];
    
    function add(this) =
        (var args) >> skip(1) >>
            where(arg => Not store.Contains(arg)) >>
            each(arg => store.Add(arg));
    
    function remove(this) = 
        (var args) >> skip(1) >>
            forall(arg => store.Remove(arg));
            
    function contains(this) = 
        (var args) >> skip(1) >>
            forall(arg => store.Contains(arg));
        
    function count =
        store.Count;
        
    function GetEnumerator =
        store.GetEnumerator;        
        
    add(var args);
    
    return struct;    
}

function create_immutable_set(elements)[is volatile;]
{
    elements = if(elements is Null) [] else all(elements); //all makes a copy
    
    //Actions
    function add(this) = 
        ((var args) >> skip(1)) + elements >> distinct >> create_immutable_set;
        
    function remove(this) =
        elements >> except((var args) >> skip(1)) >> create_immutable_set;
    
    //Queries
    function contains(this) =
        (var args) >> skip(1) >>
            forall(arg => elements.Contains(arg));
            
    function count = 
        elements.Count; 
        
    function GetEnumerator =
        elements.GetEnumerator;
        
    return struct;                   
}
