
build does require
(
    "misc.pxs",
    "debug.pxs",
    "struct.pxs"
);

function imin(a,b) = min(a,b)~Int;
function imax(a,b) = max(a,b)~Int;

declare
    imin as min,
    imax as max;

function create_container [is volatile;]
{   
    var container = [];
     
    function register(this) does for(var i = 1; i < var args.Length; i++)
    {
        var arg = args[i];
        if(arg is Null Or container.Contains(arg))
            continue;
        container.Add(arg);
    }
    
    function remove does for(var i = 1; i < var args.Length; i++)
    {
        var arg = args[i];
        if(arg is Null)
            continue;
        container.Remove(arg);
        
    }
    
    function update does
        foreach(var control in container)
            control.update;
            
    function clear does
        foreach(var control in container)
            control.clear;
    
    function registered(this,control) =
        create_terminator(register(this) = control, 
                    () => remove  (this) = control);
                    
    return struct;
}
        
var shared_container = create_container;

function register_control does foreach(var arg in var args)
    shared_container.register(arg);

function remove_control does foreach(var arg in var args)
    shared_container.remove(arg);

function update_controls does shared_container.update;
        
function clear_controls does shared_container.clear;

function registered_control control = shared_container.registered(control);

function create_register_function ctor => () => register_control = call(ctor,var args);
function create_registered_function ctor => () => registered_control = call(ctor,var args);
        
function create_field(x,y,length)
[ import System; is volatile; ]
{
    var content = "";
    length ??= -1;    
    var needClear = 0;
    
    x~=Int;
    y~=Int;
    length~=Int;
    
    function text(this,newContent)
    {
        if(Not newContent is Null)
        {
            newContent = newContent.ToString;
            needClear = content.Length - newContent.Length; //need to clear if positive
            content = 
                if(length < 0)
                    newContent
                else
                    newContent.Substring(0,min(length,newContent.Length));
        }
        return content;
    }
    
    function do_update(action)
    [ is private; ]
    {
        action ??= () => 
        {
            ::Console.Write(content);
            if(needClear > 0)
            {
                ::Console.Write(" " * needClear);
                needClear = 0;
            }
        };
            
        var ot = ::Console.CursorTop;
        var ol = ::Console.CursorLeft;
        var ov = ::Console.CursorVisible;
        
        try
        {        
            ::Console.CursorVisible = false;
            /*
            if(y < 0)
            {
                y = -y;
                //Move content up
                ::Console.MoveBufferArea(
                    0,::Console.WindowTop+y,
                    ::Console.BufferWidth,::Console.WindowHeight-y,
                    0,::Console.WindowTop
                );
                y -= 1;
                ot -= y;                
            } */
            ::Console.CursorTop = ot - y;
            ::Console.CursorLeft = x;
            action.();
        }
        finally
        {
            ::Console.CursorTop = ot;
            ::Console.CursorLeft = ol;
            ::Console.CursorVisible = ov;
        }
    }
        
    function update(this, newT)
    {
        this.text = newT;
        do_update;
    }
    
    function clear does do_update = () => 
    {
        ::Console.Write(content.Length * " ");
        needClear = 0;
    };
    
    function ToString = "Field at ($x,$y), displaying \"$(content.ToString)\"";
    
    return struct; 
}

function create_progressbar(x,y,length) [is volatile;]
{
    length~=Int;
    var field = create_field(x,y,length);
    if(debug)
        var d = create_field(x+length+2,y);
    var per = 0.0;
    var anim = ["/","-","\\","|"];
    function animlen = anim.Count;
    var idx = 0;
    
    function animation this newAnim
    {
        if(Not newAnim is Null)
            anim = newAnim~List;
        return anim;
    }
    
    function percent(this, newPer)
    {
        if(Not newPer is Null)
        {
            newPer ~=Real;
            if(newPer < 0.0)
                newPer = 0.0;
            else if(newPer > 1.0)
                newPer = 1.0;
            per = newPer;   
        }
        return per;
    }
    
    function nextAnim = anim[(idx = (idx+1) mod animlen)];
    
    function update(this, newPer)
    {
        this.percent = newPer;
        var barlen = length - 2;
        var completed = ::Math.Floor((barlen * per)~System::Double)~Int;
        if(debug)
        {
            d.text = "length $length, percent $(round(per*100))%, completed $completed";
            d.update; 
        }
        var thisAnim = nextAnim;
        if(per == 1.0)
            field.update = "[" + (barlen)*"=" + "]";
        else
            field.update = "[" + setright(completed,thisAnim,"=") + max(barlen-max(completed,thisAnim.Length),0)*"_" + "]";
    }
    
    function clear does field.clear;
    
    return struct;
}

ref register_field = create_register_function(->create_field);
ref registered_field = create_registered_function(->create_field);
ref register_progressbar = create_register_function(->create_progressbar);
ref registered_progressbar = create_registered_function(->create_progressbar);