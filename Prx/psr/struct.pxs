var struct\ordinalIc = System::StringComparison.OrdinalIgnoreCase;

function struct
{
    function getId(g) =
        if(g.Meta.ContainsKey("SId"))
            g.Meta["SId"].Text
        else if(g.Meta.ContainsKey("LogicalId"))
            g.Meta["LogicalId"].Text
        else
            throw "The function $g cannot be used in a" +
                " struct because it has no LogicalId";
        ;
    
    var parentId = caller.Implementation.Id;
    var methods = (asm(ldr.app).Functions) 
        >> where(f => 
            f
                .Meta["ParentFunction"]
                .Text
                .Equals(parentId, struct\ordinalIc)            
            And Not f.Meta["Private"].Switch)
        >> map(f => getId(f): 
            caller
                .LocalVariables[f.Meta["LogicalId"].Text]
                .Value)
        >> where(fcf => Not fcf.Value is Null);
    
    var s = new Structure;
        
    foreach(var fcf in methods)
        s.\\(fcf.Key) = fcf.Value;
        
    return s;
}