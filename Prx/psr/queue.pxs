
build does require
(
    @"psr\struct.pxs"
);

function create_queue(lst)
{
    if(lst is Null)
        lst = [];
    else if(Not lst is List)
        lst = lst >> all;
    
    function enqueue this x = create_queue(lst + [x]);
    
    function count this = lst.Count;
    
    function peek this = if(lst.Count > 0) lst[0] else null;
    
    function dequeue this, ref item
    {
        if(lst.Count < 1)
            throw "Queue is empty!";
        item = lst[0];
        return create_queue(all << skip(1) << lst);
    }
    
    function ToString this = "queue $lst";
    
    return struct;
}

function create_mutable_queue(lst)
{
    var q = create_queue(lst);
    
    function enqueue this
    {
        foreach(var x in var args >> skip(1))
            q = q.enqueue(x);
    }
    
    function peek this = q.peek;
    
    function count this = q.count;
    
    function dequeue this
    {
        var item;
        q = q.dequeue(->item);
        return item;
    }
    
    function clear does q = new queue;
    
    function ToString this = "mutable $q";
    
    function GetEnumerator
    {
        coroutine dq'ing_sequence
        {
            var z;
            while(q.count > 0)
                yield dequeue;
        }
        return dq'ing_sequence.GetEnumerator;
    }
    
    return struct;
}

declare 
    create_queue as create_immutable_queue;
