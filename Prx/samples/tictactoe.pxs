Name TicTacToe;
Description "A tic tac toe game for two players.";
Author SealedSun;

Entry main;

//Just some shorthands
var X = "X";
var O = "O";
var _ = " ";

//The Fields
var fields = [  _, _, _,
                _, _, _,
                _, _, _ 
             ];

//Counts the number of empty fields
function emptyFields
{
    var n = 0;
    foreach(var f in fields)
        if(f == _)
            n++;
    return n;
}

//Translate a string address to an index
function address(s)
{
    //Ensure "s" has the right format
    s = s.Trim.ToUpper;
    if(s.Length != 2)
        return -1;
    //Read row
    var a = 0;
    var letter = s.Substring(0,1);
    if(letter == "B")
        a += 3;
    else if(letter == "C")
        a += 6;
    
    //Read column
    var number = ~Int.Parse(s.Substring(1,1)) - 1;
    a += number < 3 ? number >= 0 ? number : 0 : 2;
    return a > 8 ? 8 : a;
}

//Create shorthands for fields
function accessor(index) = v => 
{
    return = fields[index];
    if(v != null)
        fields[index] = v;
};

//The actual shortcuts
ref a1 = accessor(0); ref b1 = accessor(3); ref c1 = accessor(6);
ref a2 = accessor(1); ref b2 = accessor(4); ref c2 = accessor(7);
ref a3 = accessor(2); ref b3 = accessor(5); ref c3 = accessor(8);

//Draw the fields
function draw_fields does print =
    "     1   2   3\n" +
    "    --- --- --- \n"+
    " a | $a1 | $a2 | $a3 |\n"+
    "    --- --- --- \n"+
    " b | $b1 | $b2 | $b3 |\n"+
    "    --- --- --- \n"+
    " c | $c1 | $c2 | $c3 |\n"+
    "    --- --- --- \n";   


var rnd = new System::Random;
function randomAddress
{
    if(emptyFields == 0)
        return "b2";    
    do
    {
        var r = rnd.Next(0,3);
        var a = (r != 2 ? r != 1 ? "a" : "b" : "c") + rnd.Next(1,4).ToString;
    }until(fields[address(a)] == _);
    return a;
}

//Ask the player to enter a field address
function choose_field does
    do
    {
        print("Please enter a field address (e.g., $randomAddress): ");   
        var r = address(::Console.ReadLine);
        return = r;
    }until(r > -1)

function main
{
    println("** Tic Tac Toe **");
    var isPlayer1 = false;
    var symbol;
    //Determines if all three fields contain the same symbol
    function line(f1, f2, f3) = 
        f1 == symbol && 
        f2 == symbol && 
        f3 == symbol;
    
    do
    {
        //Swap players
        isPlayer1 = Not isPlayer1;
        draw_fields;
        //Ask for the next move
        println("It's your turn, player " + (isPlayer1 ? "1" : "2") + ".");
        do
            var chosen = choose_field;
        until(fields[chosen] == _)
        
        var winner = -1;
        symbol = isPlayer1 ? X : O;        
        fields[chosen] = symbol; //"symbol" is used by the function "line"
         
        if(//Horizontal      //Vertical        //Diagonal
           line(a1,a2,a3) || line(a1,b1,c1) || 
           line(b1,b2,b3) || line(a2,b2,c2) || line(a1,b2,c3) ||
           line(c1,c2,c3) || line(a3,b3,c3) || line(a3,b2,c1) ) 
                winner = isPlayer1 ? 1 : 2;
        
        //Game is drawn if no player won and there are no fields left.
        if(winner == -1 && emptyFields == 0)
            winner = 0;
    }until(winner >= 0)
    
    //Present the results
    draw_fields;
    if(winner == 0)
        println("There is no winner...");
    else
        println("Player $winner won the game!");
}